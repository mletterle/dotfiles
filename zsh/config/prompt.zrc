_mdl_new_line() {
  print ""
}

_mdl_vcs_precmd() {
  _MDL_GIT_UPSTREAM=$(git rev-parse --abbrev-ref @{u} 2> /dev/null)
  if [ ! $? -eq 0 ]; then
    _MDL_GIT_REMOTE_PART=
  else
    _MDL_GIT_REMOTE_PART="$(echo -n ${_MDL_GIT_UPSTREAM} | awk -F'/' '{print $(NF-1)}')/"
  fi

  _MDL_VCS_INFO=$'\n'
  if [[ -n "${vcs_info_msg_0_}" ]]; then
    _MDL_VCS_INFO="$(print -P ${vcs_info_msg_0_})"$'\n'
  fi
}


autoload -Uz add-zsh-hook

add-zsh-hook precmd vcs_info
add-zsh-hook precmd _mdl_vcs_precmd

add-zsh-hook preexec _mdl_new_line

setopt prompt_subst

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git*' formats '%F{208}îœ‚ %F{244}%r%F{240}:%F{240}(%F{244}${_MDL_GIT_REMOTE_PART}%b%F{240})%f'
zstyle ':vcs_info:git*' actionformats '%F{208}îœ‚ %F{244}%r%F{240}:%F{240}(%F{244}${_MDL_GIT_REMOTE_PART}%b%F{240}|%F{red}%a%F{240})%f'
zstyle ':vcs_info:git*' stagedstr '%F{green}+%f'
zstyle ':vcs_info:git*' unstagedstr '%F{red}*%f'
zstyle ':vcs_info:*:*' check-for-changes true


_MDL_PROMPT_VCS_INFO='${_MDL_VCS_INFO}'
_MDL_PROMPT_EXIT_IND='%(?.%K{blue} %K{249}%F{blue}î‚°.%K{red} %K{249}%F{red}î‚°) '
_MDL_PROMPT_MACHINE='%K{58}%F{249}î‚° ï’³ %m %K{179}%F{58}î‚° '
_MDL_PROMPT_USER='%F{179}î‚°%f%K{179}%F{255}ï“¿ %n %F{179}%K{12}î‚° '
_MDL_PROMPT_DIR='%F{255} %1~ %k%F{12}î‚°%f '
_MDL_PROMPT_INPUT='%(!.ðŸ¦¸.ðŸ§™ðŸª„)'

typeset -A _mdl_prompt_parts

_mdl_prompt_parts=(
  [V]="${_MDL_PROMPT_VCS_INFO}"
  [E]="${_MDL_PROMPT_EXIT_IND}"
  [M]="${_MDL_PROMPT_MACHINE}"
  [U]="${_MDL_PROMPT_USER}"
  [D]="${_MDL_PROMPT_DIR}"
  [I]="${_MDL_PROMPT_INPUT}"
)

_MDL_PROMPT="VEMUDI"

mdl-prompt-toggle-part() {
  if [[ "${_MDL_PROMPT}" == *"${1:u}"* ]]; then
   _MDL_PROMPT="$(echo $_MDL_PROMPT | tr ${1:u} ${1:l})"
  else
   _MDL_PROMPT="$(echo $_MDL_PROMPT | tr ${1:l} ${1:u})"
  fi
}

mdl-prompt-toggle-user() {
  mdl-prompt-toggle-part "U"
}

_mdl_prompt() {
  PROMPT=
  for flag in "${(s::)_MDL_PROMPT//[a-z]/}"; do
    PROMPT+=$_mdl_prompt_parts[$flag]""
  done

}

add-zsh-hook precmd _mdl_prompt
